//**** 安裝相關 *************************************************************************
//---- 安裝中的訊息 ---------------------------------------------------------------------
OnInstallBegin
{
	"\0\s[0]文件安裝開始。\e"
}

//---- 完裝完成的訊息 -------------------------------------------------------------------
//reference1：安裝成功時安裝的種類（shell / ghost / balloon / plugin / headline /supplement）。
//reference1：成功時安裝內容。
OnInstallComplete
{
	"\0\s[0]"
	--
	if reference0 == "shell"
	{
		"外表"
	}
	elseif reference0 == "ghost"
	{
		"人格"
	}
	elseif reference0 == "balloon"
	{
		"對話框"
	}
	elseif reference0 == "plugin"
	{
		"外掛"
	}
	--
	"“%(reference1)”安裝完畢。"
	--
	if reference0 == "shell" || reference0 == "shell with balloon"
	{
		"\![reloadsurface]"
	}
	--
	"\e"
}

//---- 安裝失敗的訊息 --------------------------------------------------
//reference0：安裝失敗的原因。
OnInstallFailure
{
	"\0\s[4]收件失敗…\w5…\w5\n\n（原因：%(reference0)。）\e"
}

//---- 安裝拒絕（文件為其他人格專用） -------------------------
//當安裝文件中install.txt含有accept項目，而該項目的內容不是主人格的名字，發生此事件。
//reference0：accept項目的內容（安裝文件指定的對象）
OnInstallRefuse
{
	"\0\s[0]文件安裝失敗。\w9\w9/
	\1\s[10]這是%(reference0)的專用安裝文件。\e"
}


//**** 消滅關連 *******************************************************************
//---- 消滅指示 -------------------------------------------------------------------
//右鍵菜單從上往下數第四個項目被點擊
OnVanishSelecting
{
	"\0\s[0]刪除人格選項。\w9\w9/
	\1\s[10]如果在彈出的對話框選擇“是”，\w9就會刪除本人格。\e"
}

//---- 消滅開始 --------------------------------------------------------------------
//在刪除確認的對話框選擇“是”，也就是真的刪掉人格了的情況。
OnVanishSelected
{
	"\0\s[0]刪除人格開始的對話。\w9\w9/
	\1\s[10]一般情況下的人格都會說最後的道別，\w9\w9但因為作者沒有寫這個內容，\w9所以我們也沒有什麽對話。\w9\w9當然，\w9也沒有什麽傷心的表情之類。\w9\w9/
	\0\s[0]沒有就是沒有。\w9\w9\e"
}

//---- 消滅中止 ---------------------------------------------------------------------
//在刪除確認的對話框選擇否。
OnVanishCancel
{
	"\0\s[0]取消刪除的對話。\w9\w9/
	\1\s[10]一般情況下的人格都會表示松一口氣之類的。\w9\w9我們沒有寫對應的對話，\w9就當做已經說過了吧。\e"
}

//**** 信件確認 **************************************************************************
//使用這個功能得先在SSP設置里搞定。
//---- 信件確認開始 ---------------------------------------------------------------------
OnBIFFBegin
{
"\0\s[0]確認信件。\e"
}

//---- 信件確認成功 ---------------------------------------------------------------------
OnBIFFComplete
{
	if reference0 == 0
	{
		//---- 沒有信件
		"\0\s[0]沒有新信件的對話。\e"
	}
	else
	{
		//---- 有新信件
		"\0\s[0]有%(reference0)封新信件。\e"
	}
}

//---- 信件確認失敗 ------------------------------------------------------------
//reference0：失敗理由。
OnBIFFFailure
{
	if reference0 == "timeout"
	{
		//---- 逾時
		"\0\s[0]鏈接超時。\e"
	}
	elseif reference0 == "kick"
	{
		//---- 踢除
		"\0\s[0]被踢出伺服器了。\e"
	}
	elseif reference0 == "defect"
	{
		//---- 設定錯誤
		"\0\s[0]失敗。需要重新檢查郵件設定。\e"
	}
	else
	{
		//---- 原因不明
		"\0\s[0]失敗。\w9原因不明。\e"
	}
}


//**** 線上更新************************************************************
//线上更新的地址。沒有請清空。
On_homeurl
{
	//"http://xuwu.test/ukagaka"
}
//---- 線上更新開始 -------------------------------------------------------
OnUpdateBegin
{
	"\0\s[0]線上更新開始的對話。\e"
}

//---- 線上更新準備完畢 ------------------------------------------------------
//reference0：需要更新的文件數-1（從0開始）
OnUpdateReady
{
	_a = reference0 + 1
	"\0\s[0]總共有%(_a)個文件需要更新。\e"
}

//---- 線上更新結束--------------------------------------------------------------
//reference0：成功理由。none：沒有更新
OnUpdateComplete
{
	if reference0 == "none"
	{
		//---- 沒有更新檔
		"\0\s[0]沒有任何更新時的對話。\e"
	}
	else
	{
		//---- 實際更新完成
		"\0\s[0]更新成功的對話。\e"
	}
}

//---- 線上更新失敗 ------------------------------------------------------------------
//reference0：失敗原因。
//reference1：失敗的文件名。
OnUpdateFailure
{
	"\0\s[0]線上更新失敗。\n原因："
	--
	if reference0 == "timeout"
	{
		//---- 連線逾時
		"超時。"
	}
	elseif reference0 == "md5 miss"
	{
		//---- MD5値不一致
		"MD5值錯誤。"
	}
	else
	{
		//---- 上述以外的狀況
		"%(reference0)。"
	}
	--
	"\e"
}

//---- 線上更新 下載開始 -------------------------------------------------------------
//reference0：正在下載的文件名。
//reference1：更新到現在的文件數目。
//reference2：更新文件總數。
OnUpdate.OnDownloadBegin
{
	"\0\s[0]文件 “%reference0”下載開始。\e"
}

//---- 線上更新 MD5値對照開始 ----------------------------------------------------
//reference0：文件名。
//reference1：正確的MD5
//reference2：所下載文件的MD5
OnUpdate.OnMD5CompareBegin
{
	"\0\s[0]MD5値對照開始。\e"
}

//---- 線上更新 MD5値一致 ---------------------------------------------------------------
OnUpdate.OnMD5CompareComplete
{
	"\0\s[0]一致。\e"
}

//---- 線上更新 MD5値不一致 -----------------------------------------------------
OnUpdate.OnMD5CompareFailure
{
	"\0\s[0]MD5不一致。\e"
}

//**** HeadlineSense ******************************************************************************
//---- HeadlineSense開始 ----------------------------------------------------------------
//reference0：站點名稱。
//reference1：站點URL。
OnHeadlinesenseBegin
{
	"\0\s[0]Headline“%(reference0)”取得中。\e"
}

//---- Headline讀取 ---------------------------------------------------------------------
OnHeadlinesense.OnFind
{
	//---- 首頁對白
	if reference2 == "First" || reference2 == "First and Last"
	{
		//---- 首頁
		"\0\b[2]\s[0]%(reference0)的Headline。"
	}
	elseif reference2 == "Next"
	{
		//---- 中間頁
		"\0\b[2]\s[0]%(reference0)的Headline，續頁。"
	}
	elseif reference2 == "Last"
	{
		//---- 末頁
		"\0\b[2]\s[0]%(reference0)的Headline、最後一頁。"
	}
	--
	//---- Headline本体
	"\n\n%(reference3)\n\n"
	--
	//---- 選擇鍵
	if reference2 == "First" || reference2 == "Next"
	{
		//---- 有下一頁的場合
		"\q0[][下一頁]/
		\q1[%(reference1)][網址]/
		\q2[HEADLINECANCEL][取消]\e"
	}
	else
	{
		//---- 沒下一頁的場合
		"\q0[%(reference1)][網址]/
		\q1[HEADLINECANCEL][取消]\e"
	}
}

//---- Headline無更新 -------------------------------------------------------------------
OnHeadlinesenseComplete
{
	"\0\s[0]%(reference0)的headline沒有更新。\e"
}

//---- HeadlineSense失敗 ----------------------------------------------------------------
OnHeadlinesenseFailure
{
	"\0\s[0]Headline更新失敗。\n\n理由："
	--
	if reference0 == "can't download"
	{
		"下載失敗。"
	}
	elseif reference0 == "can't analyze"
	{
		"Headline解析失敗。"
	}
	else
	{
		"不明。"
	}
	--
	"\e"
}


//**** 檔案作成關連 *******************************************************************************
//---- updates2.dau作成開始 -------------------------------------------------------------
//在便利機能——人格瀏覽中右鍵人格——建立更新檔。
OnUpdatedataCreating
{
	"\0\s[0]線上更新檔案作成中。\e"
}

//---- updates2.dau作成完了 -------------------------------------------------------------
OnUpdatedataCreated
{
	"\0\s[0]線上更新檔案作成完畢。\e"
}

//---- NAR作成開始 ----------------------------------------------------------------------
OnNarCreating
{
	"\0\s[0]nar檔作成開始。\e"
}

//---- NAR作成完了 ----------------------------------------------------------------------
OnNarCreated
{
	"\0\s[0]“%(reference0)”的nar檔“%(reference1)”完成。\e"
}

//**** 時間同步處理 *******************************************************************************
//**** みか (http://homepage2.nifty.com/ko-ki/) の辞書を参考にしました。感謝＞こーき氏

Choice.SNTPTIME
{
	"\0\s[0]現在的校準伺服器是"
	--
	if SNTPserver == ""
	{
	"默認伺服器"
	}
	else
	{
	"\n\_q%(SNTPserver)\_q"
	}
	--
	"\n要更換伺服器嗎？\n\n/
	\![*]\q[校準開始,SNTPStart]\n/
	\![*]\q[更改伺服器,SNTPChange]\e"
}
Choice.SNTPChange
{
	"\0\s[0]輸入新的伺服器地址。\![open,inputbox,sntpserver,-1,stdtime.gov.hk]\e"
}
Choice.SNTPStart
{
	"\![executesntp]\e"
}
//---- 時間同步處理開始 -----------------------------------------------------------------
OnSNTPBegin
{
	reference0 = SNTPserver;
	"\0\s[0]（連線到\_q%(reference0)\_q伺服器，\w8\w8時間同步開始。）\w8\w8\e"
}


//---- 時刻取得成功、比較開始 -----------------------------------------------------------
OnSNTPCompare
{
	"\0\s[0]\n"
	--
	if reference3 == 0
	{
		_nowtime = SPLIT(reference1,',');
		"沒有誤差。\w8\w9\n現在時刻：%(_nowtime[0])年%(_nowtime[1])月%(_nowtime[2])日 %(_nowtime[3]):%(_nowtime[4])\e"
	}
	else
	{
		"誤差%(reference3)秒。\w8\w8\n\n\s[0]校準中。\w8\w8\w8\6\e"
	}
}

OnSNTPCorrect
{
	_nowtime = SPLIT(reference1,',');
	"\0\s[0]同步完成。\w8\w8\n現在時刻：%(_nowtime[0])年%(_nowtime[1])月%(_nowtime[2])日%(_nowtime[3])：%(_nowtime[4])\e"
}


//---- 時間同步處理失敗 -----------------------------------------------------------------
OnSNTPFailure
{
	"\0\s[0]時間同步失敗。\e"
}



//**** 其他處理 ***********************************************************************************

//---- 顯示解析度變更 -----------------------------------------------------------
OnDisplayChange
{
	//---- 用變數取得顯示解析度與色深度相關情報
	//---- 不知如何使用的話請保持原樣
	displaybpp    = reference0
	displaywidth  = reference1
	displayheight = reference2
}

OnURLDropping
{
	"\0\s[0]\_q%(reference0)資料取得中。\_q\e"
}

OnURLDropped
{
	"\0\s[0]\_q%(reference0)資料取得完成。\_q\e"
}

OnURLDropFailure
{
	"\0\s[4]\_q%(reference0)取得失敗。\n\n原因：%(reference1)\_q\e"
}

//---- 電池電力不足 --------------------------------------------------------------
//reference0：剩餘電量百分比。不明時-1。
//reference1：剩餘電量時間。不明時-1。
OnBatteryLow
{
"\0\s[0]筆記本電腦電池電力不足的對話。\e"
}
//---- windows電源管理設定通知電源不足----------------------------------
//reference0：剩餘電量百分比。不明時-1。
//reference1：剩餘電量時間。不明時-1。
OnBatteryCritical
{
"\0\s[0]筆記本電腦電池電力非常不足的對話。\e"
}

//---- 螢幕保護程式實行開始 -------------------------------------------------------------
OnScreenSaverStart
{
	"\0\s[0]螢幕保護程式實行開始的對話。\e"
}

//---- 螢幕保護程式實行終了 -------------------------------------------------------------
OnScreenSaverEnd
{
	"\0\s[0]螢幕保護程式實行終了的對話\e"
}

//**** 泛用應用程式 *******************************************************************************

//---- 起動 -----------------------------------------------------------------------------
OnApplicationBoot
{
	"\0\s[0]%(reference0)起動。\e"
}

//---- 終了 -----------------------------------------------------------------------------
OnApplicationClose
{
	"\0\s[0]%(reference0)終了。\e"
}

//---- 檔案開啟 -------------------------------------------------------------------------
OnApplicationFileOpen
{
	"\0\s[0]檔案%(reference1)開啟。\e"
}

//---- 系統資源不足 ---------------------------------------------------------------
//不到40%時發生的對話。
//話說回來真的有人能看到這個對話嗎？
OnSysResourceLow
{
	"\0\s[0]系統資源不足。\e"
}
//不到20%時發生的對話。
OnSysResourceCritical
{
	"\0\s[0]系統資源非常不足。\e"
}