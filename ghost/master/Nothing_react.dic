//**** 撫摸反應 ***********************************************************************************
//---- OnMouseMove事件 ------------------------------------------------------------------
//reference0：鼠標指針的X座標
//reference1：鼠標指針的Y座標
//reference2：鼠標指針的回轉方向（？）
//reference3 ：主人格端/使魔端（用0/1表示）
//reference4 ：觸摸判定區域的名字
OnMouseMove
{
	//---- 根據reference3判定是本体側還是kero側
	if reference3 == 0
	{
		//---- 本体側的撫摸
		MouseMoveSakura
	}
	else
	{
		//---- kero側的撫摸
		MouseMoveKero
	}

	//以下是待機模式的部分
    //当鼠标在上面移动时，更新最后操作时间为当前时间
    //为了完成别的操作（如换装、双击等）都会触发移动鼠标事件，因此以该事件作为基准
    LastReactTime = GETTIME()
}

//---- 本体側の撫摸反應 -----------------------------------------------------------------
MouseMoveSakura
{
	if reference4 != ""
	{
		//---- 當撫摸人格特定位置時
		stroke++
		//---- 撫摸達到一定量時判定撫摸的部位
		if stroke >= 96
		{
			stroke = 0
			if reference4 == "face"
			{
				"请不要捏我的脸，我的脸可是用很贵的材料做出来的呢。"
				"戳我干什么？"
				"%(username)，你现在是不是很无聊呢？"
				"小心一点，我的脸可是很贵的！"
			}
			elseif reference4 == "shoulder"
			{
				"這裏是肩膀被觸摸時的對話，\w5被定義在react裏的48行。\w9請修改一下"
			}
			elseif reference4 == "head"
			{
				"这就是被摸头的感觉吗？真是一种神奇的感觉。"
				"嗯……再摸两下可以吗？"
				"我也想试试摸你的头。"
				"稍微感觉到了一丝开心呢。"
			}
			elseif reference4 == "chest"
			{
				"呀！变态！………你是不是在期待着这个反应？"
				"按理说我的胸摸起来应该是硬邦邦的，为什么你摸的那么开心？"
				"这在法律上可是性骚扰。"
				"黄牌警告。"
			}
			elseif reference4 == "hands"
			{
				"想和我牵手吗？"
				"或许我们可以击个掌什么的。"
				"你拍一，我拍一"
				"想看我的手指舞吗？想？告诉你其实我不会╮(‵▽′)╭"
			}
			elseif reference4 == "arms"
			{
				"這裏是手臂被觸摸時的對話，\w5被定義在react裏的73行。\w9請修改一下"
			}
			elseif reference4 == "belly"
			{
				"抱歉，我的肚子上并没有痒痒肉。"
				"哈哈哈，好痒，别戳了！……这样的对话你是听不到的。"
				"总是戳我肚子我也不会有特殊反应哦。"
			}
			elseif reference4 == "skirt"
			{
				"上升气流！"
				"猜猜我的裙底有什么？"
				"你是掀不起来的，相信我。"
				"若隐若现才是王道，这句话是真央告诉我的。"
			}
			elseif reference4 == "knees"
			{
				"我的膝盖看上去是不是很奇特？它的灵活性也非常棒哦。"
				"触碰的时候请放轻松，不要把它弄坏了。"
				"像人偶一样的膝盖，这个设定可以说是很赞了。"
				"摸上去的感觉怎么样？"
			}
			elseif reference4 == "legs"
			{
				"這裏是腿被觸摸時的對話，\w5被定義在react裏的97行。\w9請增加修改一下"
				"腿控吗？\w5别看这样，我还是很自信的哦？"
			}
			elseif reference4 == "hair"
			{
				"我的头发是不是很长？它们是我的骄傲。"
				"唐僧洗发用飘柔。"
				"你是不是想要弄乱我的头发？不过你放心，它们永远也不会打结。"
				"别把我的头发扯掉了，它们可不会再长出来。"
			}
			else
			{
				// 撫摸未定義部位時無反應
				stroke = 0
			}
		}
	}
}
//---- kero側の撫摸反應 -----------------------------------------------------------------
MouseMoveKero
{
		//---- 當撫摸人格特定位置時
		stroke++
		//---- 撫摸達到一定量時判定撫摸的部位
		if stroke >= 96
		{
			stroke = 0
			"\1\s[10]我沒有觸摸判定。\e"
			"\1\s[10]我沒有觸摸判定。\w9\w9\n需要反應的話摸殼就好了。\w9\w9/
			\0\s[0]我也沒有。\e"
			"\1\s[10]我沒有觸摸判定。\w9\w9\n觸摸反應的對話也沒有寫。\e"
		}
}


//**** 雙擊反應 ***********************************************************************************
//---- OnMouseDoubleClick事件 -----------------------------------------------------------
//reference0：鼠標指針的x座標
//reference1：鼠標指針的y座標
//reference2：永遠是0。（大概是爲了和鼠標移動對稱吧……
//reference3：主人格是0，使魔是1。
//reference4：判定區域的名字。
//reference5：左鍵0，右鍵1，鼠標滾輪/中鍵2。

OnMouseDoubleClick
{
	//---- 根據reference3判定是本体側還是kero側
	if reference3 == 0
	{
		//---- 本体側的雙擊
		MouseDoubleClickSakura
	}
	else
	{
		//---- kero側的雙擊
		MouseDoubleClickKero
	}
}

//---- 本体側的雙擊反應 -------------------------------------------------------------
MouseDoubleClickSakura
{
	if reference4 == "ke" //胸口的字。
	{
		"\0\s[0]這是雙擊胸口的字的對話。\w9\w9/
		\1\s[10]因為沒有寫，\w9所以就當做已經說了吧。\e"
		"\0\s[0]這是雙擊胸口的字的對話。\w9\w9/
		\1\s[10]這是雙擊胸口的字的吐槽。\e"
		"\0\s[0]這是雙擊判定區“ke”的對話。\w9\w9/
		\1\s[10]判定區的名字由shell部份的文檔決定。\e"
	}
	elseif reference4 == "mao" //頭上的毛
	{
		"\0\s[0]這是雙擊呆毛的對話。\e"
		"\0\s[0]這是雙擊呆毛的對話。\w9\w9/
		\1\s[10]這是吐槽呆毛的對話。\w9\w9/
		\0\s[0]\n\n那是什麽。\e"
	}
	else
	{
		//---- 雙擊非判定區的時候，打開人格菜單
		OpenMenu
	}
}

//---- kero側的雙擊反應 -------------------------------------------------------------
MouseDoubleClickKero
{
	"\1\s[10]我沒有雙擊反應，\w9需要反應去雙擊殼。\w9\w9/
	\0\s[0]我也沒有。\e"
	"\1\s[10]作者沒有寫我的雙擊反應。\e"
}

//———————鼠標拖拽開始（移動開始）—————————
//reference3：拖拽的對象是主人格0，使魔1
//reference4：鼠標所在判定區
//reference5：左鍵0，右鍵1
OnMouseDragStart
{

}
//———————鼠標拖拽結束（移動結束）——————————
//reference3：拖拽的對象是主人格0，使魔1
//reference4：鼠標所在判定區
//reference5：左鍵0，右鍵1
OnMouseDragEnd
{

}

//鍵盤反應————————————————
//reference0：按下的鍵。
//剩下的reference因為不常用所以這裡省略了。
OnKeyPress
{
	if reference0 == "f1" 
	{
		//---- 開啟readme.txt
		"\![open,readme]"
	}
	elseif reference0 == "c" {
		//---- 開始溝通
		"\![open,communicatebox]"
	}
	elseif reference0 == "s" {
		//---- 外表清單
		"\![open,shellexplorer]"
	}
	elseif reference0 == "r" {
		//---- 使用率
		"\![open,rateofusegraph]"
	}
	elseif reference0 == "m" {
		//---- 開菜單
		OpenMenu
	}
}

//---- SHELL更替開始 --------------------------------------------------------------------
OnShellChanging
{
	oldshell.name = shell.name
	shell.name = "%(reference0)"
	if oldshell.name == shell.name
	{
	"\0\s[0]開始重新載入外表的對話。\e"
	}
	else
	{
	"\0\s[0]開始更換外表事件。\w9\w9/
	\1\s[10]更換的外表名稱為%(shell.name)\e"
	}
}

//---- SHELL更替完了 --------------------------------------------------------------------
OnShellChanged
{
	if oldshell.name == shell.name
	{
	"\0\s[0]重新載入外表完畢的事件。\e"
	}
	else
	{
	"\0\s[0]開始更換外表事件。\w9\w9/
	\1\s[10]從外表%(oldshell.name)更換到外表%(shell.name)。\e"
	}
}

//—————————————換裝反應—————————————
//reference0：0是主人格，1是使魔
//reference1：衣物的名字（不包含分類）
//reference2：1是穿，2是脫
//…因為默認SHELL沒有衣物，這個事件當然就不寫內容了。
OnDressupChanged
{

}
